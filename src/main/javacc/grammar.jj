/*
 * Copyright (C) 2007-2010 JÃºlio Vilmar Gesser.
 * Copyright (C) 2011, 2013-2015 The JavaParser Team.
 * Copyright (C) 2011, 2015 Didier Villevalois.
 *
 * This file is part of JLaTo.
 *
 * JLaTo is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JLaTo is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with JLaTo.  If not, see <http://www.gnu.org/licenses/>.
 */

options {
  LOOKAHEAD = 1;
  UNICODE_INPUT = true;
  JAVA_UNICODE_ESCAPE = false;

  JDK_VERSION = "1.6";

  //SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
  STATIC = false;

  COMMON_TOKEN_ACTION = true;
  TOKEN_MANAGER_USES_PARSER = true;

  TOKEN_EXTENDS = "org.jlato.parser.ParserBase.TokenBase";
}

PARSER_BEGIN(ParserImpl)
/*
 * Copyright (C) 2015 Didier Villevalois.
 *
 * This file is part of JLaTo.
 *
 * JLaTo is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JLaTo is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with JLaTo.  If not, see <http://www.gnu.org/licenses/>.
 */

package org.jlato.parser;

import java.io.*;
import java.util.*;
import org.jlato.internal.bu.*;
import org.jlato.internal.bu.coll.*;
import org.jlato.internal.bu.decl.*;
import org.jlato.internal.bu.expr.*;
import org.jlato.internal.bu.name.*;
import org.jlato.internal.bu.stmt.*;
import org.jlato.internal.bu.type.*;
import org.jlato.tree.decl.ModifierKeyword;
import org.jlato.tree.expr.AssignOp;
import org.jlato.tree.expr.BinaryOp;
import org.jlato.tree.expr.UnaryOp;
import org.jlato.tree.type.Primitive;

/**
 * <p>This class was generated automatically by javacc, do not edit.</p>
 */
final class ParserImpl extends ParserBase {

    void reset(InputStream inputStream, String encoding) {
        ReInit(inputStream, encoding);
        reset();
    }

    void reset(Reader reader) {
        ReInit(reader);
        reset();
    }

    private void throwParseException(Token token, String message) throws ParseException {
        StringBuilder buf = new StringBuilder();
        buf.append(message);
        buf.append(": '");
        buf.append(token.image);
        buf.append("' at line ");
        buf.append(token.beginLine);
        buf.append(", column ");
        buf.append(token.beginColumn);
        ParseException e = new ParseException(buf.toString());
        e.currentToken = token;
        throw e;
    }
}

PARSER_END(ParserImpl)

TOKEN_MGR_DECLS:{

	void CommonTokenAction(Token token) {
		parser.postProcessToken(token);
	}
}

/* WHITE SPACE */

SPECIAL_TOKEN :
{
  <WHITESPACE: (" " | "\t")+ >
}

SPECIAL_TOKEN :
{
  <NEWLINE: ("\n" | "\r" | "\f" | "\r\n") >
}

/* COMMENTS */

SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* >
}

MORE :
{
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_JAVA_DOC_COMMENT
|
  <"/*"> : IN_MULTI_LINE_COMMENT
}

<IN_JAVA_DOC_COMMENT>
SPECIAL_TOKEN :
{
  <JAVA_DOC_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_JAVA_DOC_COMMENT, IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* RESERVED WORDS AND LITERALS */

TOKEN :
{
  < ABSTRACT: "abstract" >
| < ASSERT: "assert" >
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CATCH: "catch" >
| < CHAR: "char" >
| < CLASS: "class" >
| < CONST: "const" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < ENUM: "enum" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINAL: "final" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < GOTO: "goto" >
| < IF: "if" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < INSTANCEOF: "instanceof" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NEW: "new" >
| < NULL: "null" >
| < PACKAGE: "package">
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < SHORT: "short" >
| < STATIC: "static" >
| < STRICTFP: "strictfp" >
| < SUPER: "super" >
| < SWITCH: "switch" >
| < SYNCHRONIZED: "synchronized" >
| < THIS: "this" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRANSIENT: "transient" >
| < TRUE: "true" >
| < TRY: "try" >
| < VOID: "void" >
| < VOLATILE: "volatile" >
| < WHILE: "while" >
}

/* LITERALS */

TOKEN :
{
  < LONG_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])
      | <HEX_LITERAL> (["l","L"])
      | <OCTAL_LITERAL> (["l","L"])
      | <BINARY_LITERAL> (["l","L"])
  >
|
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL>
      | <HEX_LITERAL>
      | <OCTAL_LITERAL>
      | <BINARY_LITERAL>
  >
|
  < #DECIMAL_LITERAL: (["0"-"9"]((["0"-"9","_"])*["0"-"9"])?) >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"]((["0"-"9","a"-"f","A"-"F","_"])*["0"-"9","a"-"f","A"-"F"])?) >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"]((["0"-"7","_"])*["0"-"7"])?) >
|
  < #BINARY_LITERAL: "0" ["b","B"] (["0","1"]((["0","1","_"])*["0","1"])?) >
|
  < FLOAT_LITERAL:
        <DECIMAL_FLOAT_LITERAL>
      | <HEXADECIMAL_FLOAT_LITERAL>
  >
|
  < DOUBLE_LITERAL:
        <DECIMAL_DOUBLE_LITERAL>
      | <HEXADECIMAL_DOUBLE_LITERAL>
  >
|
  < #FLOAT_SUFFIX: ["f","F"] >
|
  < #DOUBLE_SUFFIX: ["d","D"] >
|
  < #DECIMAL_FLOAT_LITERAL:
        (<DECIMAL_LITERAL>)+ "." (<DECIMAL_LITERAL>)* (<DECIMAL_EXPONENT>)? <FLOAT_SUFFIX>
      | "." (<DECIMAL_LITERAL>)+ (<DECIMAL_EXPONENT>)? <FLOAT_SUFFIX>
      | (<DECIMAL_LITERAL>)+ <DECIMAL_EXPONENT> <FLOAT_SUFFIX>
      | (<DECIMAL_LITERAL>)+ (<DECIMAL_EXPONENT>)? <FLOAT_SUFFIX>
  >
|
  < #DECIMAL_DOUBLE_LITERAL:
        (<DECIMAL_LITERAL>)+ "." (<DECIMAL_LITERAL>)* (<DECIMAL_EXPONENT>)? (<DOUBLE_SUFFIX>)?
      | "." (<DECIMAL_LITERAL>)+ (<DECIMAL_EXPONENT>)? (<DOUBLE_SUFFIX>)?
      | (<DECIMAL_LITERAL>)+ <DECIMAL_EXPONENT> (<DOUBLE_SUFFIX>)?
      | (<DECIMAL_LITERAL>)+ (<DECIMAL_EXPONENT>)? <DOUBLE_SUFFIX>
  >
|
  < #DECIMAL_EXPONENT: ["e","E"] (["+","-"])? (<DECIMAL_LITERAL>)+ >
|
  < #HEXADECIMAL_FLOAT_LITERAL:
        "0" ["x", "X"] (["0"-"9","a"-"f","A"-"F"])+ (".")? <HEXADECIMAL_EXPONENT> <FLOAT_SUFFIX>
      | "0" ["x", "X"] (["0"-"9","a"-"f","A"-"F"])* "." (["0"-"9","a"-"f","A"-"F"])+ <HEXADECIMAL_EXPONENT> <FLOAT_SUFFIX>
  >
|
  < #HEXADECIMAL_DOUBLE_LITERAL:
        "0" ["x", "X"] (["0"-"9","a"-"f","A"-"F"])+ (".")? <HEXADECIMAL_EXPONENT> (<DOUBLE_SUFFIX>)?
      | "0" ["x", "X"] (["0"-"9","a"-"f","A"-"F"])* "." (["0"-"9","a"-"f","A"-"F"])+ <HEXADECIMAL_EXPONENT> (<DOUBLE_SUFFIX>)?
  >
|
  < #HEXADECIMAL_EXPONENT: ["p","P"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ( ( "\\" | "\\u005c" )
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
        | ("\\u"
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ( ( "\\" | "\\u005c" )
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
        | ("\\u"
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
          )
      )*
      "\""
  >
}

/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < AT: "@" >
}

/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < ELLIPSIS: "..." >
| < ARROW: "->" >
| < DOUBLECOLON : "::">
}

/* >'s need special attention due to generics syntax. */
TOKEN :
{
  < RUNSIGNEDSHIFT: ">>>" >
  {
     matchedToken.kind = GT;
     matchedToken.realKind = RUNSIGNEDSHIFT;
     input_stream.backup(2);
  }
| < RSIGNEDSHIFT: ">>" >
  {
     matchedToken.kind = GT;
     matchedToken.realKind = RSIGNEDSHIFT;
     input_stream.backup(1);
  }
| < GT: ">" >
}

/* QUOTE VARIABLES */

TOKEN :
{
  < NODE_VARIABLE:
    "\u0024"          // "$"
    (<PART_LETTER>)*
  >
  {
    if (!parser.quotesMode) matchedToken.kind = IDENTIFIER;
  }
|
  < NODE_LIST_VARIABLE:
    ".."
    "\u0024"          // "$"
    (<PART_LETTER>)*
  >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<PART_LETTER>)* >
|
  < #LETTER:
      [  // all chars for which Character.isIdentifierStart is true
         //"\u0024",          // "$"
         "\u0041"-"\u005a", // "A"-"Z"
         "\u005f",          // "_"
         "\u0061"-"\u007a", // "a"-"z"
         "\u00a2"-"\u00a5",
         "\u00aa",
         "\u00b5",
         "\u00ba",
         "\u00c0"-"\u00d6",
         "\u00d8"-"\u00f6",
         "\u00f8"-"\u0236",
         "\u0250"-"\u02c1",
         "\u02c6"-"\u02d1",
         "\u02e0"-"\u02e4",
         "\u02ee",
         "\u037a",
         "\u0386",
         "\u0388"-"\u038a",
         "\u038c",
         "\u038e"-"\u03a1",
         "\u03a3"-"\u03ce",
         "\u03d0"-"\u03f5",
         "\u03f7"-"\u03fb",
         "\u0400"-"\u0481",
         "\u048a"-"\u04ce",
         "\u04d0"-"\u04f5",
         "\u04f8"-"\u04f9",
         "\u0500"-"\u050f",
         "\u0531"-"\u0556",
         "\u0559",
         "\u0561"-"\u0587",
         "\u05d0"-"\u05ea",
         "\u05f0"-"\u05f2",
         "\u0621"-"\u063a",
         "\u0640"-"\u064a",
         "\u066e"-"\u066f",
         "\u0671"-"\u06d3",
         "\u06d5",
         "\u06e5"-"\u06e6",
         "\u06ee"-"\u06ef",
         "\u06fa"-"\u06fc",
         "\u06ff",
         "\u0710",
         "\u0712"-"\u072f",
         "\u074d"-"\u074f",
         "\u0780"-"\u07a5",
         "\u07b1",
         "\u0904"-"\u0939",
         "\u093d",
         "\u0950",
         "\u0958"-"\u0961",
         "\u0985"-"\u098c",
         "\u098f"-"\u0990",
         "\u0993"-"\u09a8",
         "\u09aa"-"\u09b0",
         "\u09b2",
         "\u09b6"-"\u09b9",
         "\u09bd",
         "\u09dc"-"\u09dd",
         "\u09df"-"\u09e1",
         "\u09f0"-"\u09f3",
         "\u0a05"-"\u0a0a",
         "\u0a0f"-"\u0a10",
         "\u0a13"-"\u0a28",
         "\u0a2a"-"\u0a30",
         "\u0a32"-"\u0a33",
         "\u0a35"-"\u0a36",
         "\u0a38"-"\u0a39",
         "\u0a59"-"\u0a5c",
         "\u0a5e",
         "\u0a72"-"\u0a74",
         "\u0a85"-"\u0a8d",
         "\u0a8f"-"\u0a91",
         "\u0a93"-"\u0aa8",
         "\u0aaa"-"\u0ab0",
         "\u0ab2"-"\u0ab3",
         "\u0ab5"-"\u0ab9",
         "\u0abd",
         "\u0ad0",
         "\u0ae0"-"\u0ae1",
         "\u0af1",
         "\u0b05"-"\u0b0c",
         "\u0b0f"-"\u0b10",
         "\u0b13"-"\u0b28",
         "\u0b2a"-"\u0b30",
         "\u0b32"-"\u0b33",
         "\u0b35"-"\u0b39",
         "\u0b3d",
         "\u0b5c"-"\u0b5d",
         "\u0b5f"-"\u0b61",
         "\u0b71",
         "\u0b83",
         "\u0b85"-"\u0b8a",
         "\u0b8e"-"\u0b90",
         "\u0b92"-"\u0b95",
         "\u0b99"-"\u0b9a",
         "\u0b9c",
         "\u0b9e"-"\u0b9f",
         "\u0ba3"-"\u0ba4",
         "\u0ba8"-"\u0baa",
         "\u0bae"-"\u0bb5",
         "\u0bb7"-"\u0bb9",
         "\u0bf9",
         "\u0c05"-"\u0c0c",
         "\u0c0e"-"\u0c10",
         "\u0c12"-"\u0c28",
         "\u0c2a"-"\u0c33",
         "\u0c35"-"\u0c39",
         "\u0c60"-"\u0c61",
         "\u0c85"-"\u0c8c",
         "\u0c8e"-"\u0c90",
         "\u0c92"-"\u0ca8",
         "\u0caa"-"\u0cb3",
         "\u0cb5"-"\u0cb9",
         "\u0cbd",
         "\u0cde",
         "\u0ce0"-"\u0ce1",
         "\u0d05"-"\u0d0c",
         "\u0d0e"-"\u0d10",
         "\u0d12"-"\u0d28",
         "\u0d2a"-"\u0d39",
         "\u0d60"-"\u0d61",
         "\u0d85"-"\u0d96",
         "\u0d9a"-"\u0db1",
         "\u0db3"-"\u0dbb",
         "\u0dbd",
         "\u0dc0"-"\u0dc6",
         "\u0e01"-"\u0e30",
         "\u0e32"-"\u0e33",
         "\u0e3f"-"\u0e46",
         "\u0e81"-"\u0e82",
         "\u0e84",
         "\u0e87"-"\u0e88",
         "\u0e8a",
         "\u0e8d",
         "\u0e94"-"\u0e97",
         "\u0e99"-"\u0e9f",
         "\u0ea1"-"\u0ea3",
         "\u0ea5",
         "\u0ea7",
         "\u0eaa"-"\u0eab",
         "\u0ead"-"\u0eb0",
         "\u0eb2"-"\u0eb3",
         "\u0ebd",
         "\u0ec0"-"\u0ec4",
         "\u0ec6",
         "\u0edc"-"\u0edd",
         "\u0f00",
         "\u0f40"-"\u0f47",
         "\u0f49"-"\u0f6a",
         "\u0f88"-"\u0f8b",
         "\u1000"-"\u1021",
         "\u1023"-"\u1027",
         "\u1029"-"\u102a",
         "\u1050"-"\u1055",
         "\u10a0"-"\u10c5",
         "\u10d0"-"\u10f8",
         "\u1100"-"\u1159",
         "\u115f"-"\u11a2",
         "\u11a8"-"\u11f9",
         "\u1200"-"\u1206",
         "\u1208"-"\u1246",
         "\u1248",
         "\u124a"-"\u124d",
         "\u1250"-"\u1256",
         "\u1258",
         "\u125a"-"\u125d",
         "\u1260"-"\u1286",
         "\u1288",
         "\u128a"-"\u128d",
         "\u1290"-"\u12ae",
         "\u12b0",
         "\u12b2"-"\u12b5",
         "\u12b8"-"\u12be",
         "\u12c0",
         "\u12c2"-"\u12c5",
         "\u12c8"-"\u12ce",
         "\u12d0"-"\u12d6",
         "\u12d8"-"\u12ee",
         "\u12f0"-"\u130e",
         "\u1310",
         "\u1312"-"\u1315",
         "\u1318"-"\u131e",
         "\u1320"-"\u1346",
         "\u1348"-"\u135a",
         "\u13a0"-"\u13f4",
         "\u1401"-"\u166c",
         "\u166f"-"\u1676",
         "\u1681"-"\u169a",
         "\u16a0"-"\u16ea",
         "\u16ee"-"\u16f0",
         "\u1700"-"\u170c",
         "\u170e"-"\u1711",
         "\u1720"-"\u1731",
         "\u1740"-"\u1751",
         "\u1760"-"\u176c",
         "\u176e"-"\u1770",
         "\u1780"-"\u17b3",
         "\u17d7",
         "\u17db"-"\u17dc",
         "\u1820"-"\u1877",
         "\u1880"-"\u18a8",
         "\u1900"-"\u191c",
         "\u1950"-"\u196d",
         "\u1970"-"\u1974",
         "\u1d00"-"\u1d6b",
         "\u1e00"-"\u1e9b",
         "\u1ea0"-"\u1ef9",
         "\u1f00"-"\u1f15",
         "\u1f18"-"\u1f1d",
         "\u1f20"-"\u1f45",
         "\u1f48"-"\u1f4d",
         "\u1f50"-"\u1f57",
         "\u1f59",
         "\u1f5b",
         "\u1f5d",
         "\u1f5f"-"\u1f7d",
         "\u1f80"-"\u1fb4",
         "\u1fb6"-"\u1fbc",
         "\u1fbe",
         "\u1fc2"-"\u1fc4",
         "\u1fc6"-"\u1fcc",
         "\u1fd0"-"\u1fd3",
         "\u1fd6"-"\u1fdb",
         "\u1fe0"-"\u1fec",
         "\u1ff2"-"\u1ff4",
         "\u1ff6"-"\u1ffc",
         "\u203f"-"\u2040",
         "\u2054",
         "\u2071",
         "\u207f",
         "\u20a0"-"\u20b1",
         "\u2102",
         "\u2107",
         "\u210a"-"\u2113",
         "\u2115",
         "\u2119"-"\u211d",
         "\u2124",
         "\u2126",
         "\u2128",
         "\u212a"-"\u212d",
         "\u212f"-"\u2131",
         "\u2133"-"\u2139",
         "\u213d"-"\u213f",
         "\u2145"-"\u2149",
         "\u2160"-"\u2183",
         "\u3005"-"\u3007",
         "\u3021"-"\u3029",
         "\u3031"-"\u3035",
         "\u3038"-"\u303c",
         "\u3041"-"\u3096",
         "\u309d"-"\u309f",
         "\u30a1"-"\u30ff",
         "\u3105"-"\u312c",
         "\u3131"-"\u318e",
         "\u31a0"-"\u31b7",
         "\u31f0"-"\u31ff",
         "\u3400"-"\u4db5",
         "\u4e00"-"\u9fa5",
         "\ua000"-"\ua48c",
         "\uac00"-"\ud7a3",
         "\ud801", //for supplementary characters suport
         "\ud802", //for supplementary characters suport
         "\uf900"-"\ufa2d",
         "\ufa30"-"\ufa6a",
         "\ufb00"-"\ufb06",
         "\ufb13"-"\ufb17",
         "\ufb1d",
         "\ufb1f"-"\ufb28",
         "\ufb2a"-"\ufb36",
         "\ufb38"-"\ufb3c",
         "\ufb3e",
         "\ufb40"-"\ufb41",
         "\ufb43"-"\ufb44",
         "\ufb46"-"\ufbb1",
         "\ufbd3"-"\ufd3d",
         "\ufd50"-"\ufd8f",
         "\ufd92"-"\ufdc7",
         "\ufdf0"-"\ufdfc",
         "\ufe33"-"\ufe34",
         "\ufe4d"-"\ufe4f",
         "\ufe69",
         "\ufe70"-"\ufe74",
         "\ufe76"-"\ufefc",
         "\uff04",
         "\uff21"-"\uff3a",
         "\uff3f",
         "\uff41"-"\uff5a",
         "\uff65"-"\uffbe",
         "\uffc2"-"\uffc7",
         "\uffca"-"\uffcf",
         "\uffd2"-"\uffd7",
         "\uffda"-"\uffdc",
         "\uffe0"-"\uffe1",
         "\uffe5"-"\uffe6"
      ]
  >
|
  < #PART_LETTER:
      [  // all chars for which Character.isIdentifierPart is true
         "\u0000"-"\u0008",
         "\u000e"-"\u001b",
         "\u0024",          // "$"
         "\u0030"-"\u0039", // "0"-"9"
         "\u0041"-"\u005a", // "A"-"Z"
         "\u005f",          // "_"
         "\u0061"-"\u007a", // "a"-"z"
         "\u007f"-"\u009f",
         "\u00a2"-"\u00a5",
         "\u00aa",
         "\u00ad",
         "\u00b5",
         "\u00ba",
         "\u00c0"-"\u00d6",
         "\u00d8"-"\u00f6",
         "\u00f8"-"\u0236",
         "\u0250"-"\u02c1",
         "\u02c6"-"\u02d1",
         "\u02e0"-"\u02e4",
         "\u02ee",
         "\u0300"-"\u0357",
         "\u035d"-"\u036f",
         "\u037a",
         "\u0386",
         "\u0388"-"\u038a",
         "\u038c",
         "\u038e"-"\u03a1",
         "\u03a3"-"\u03ce",
         "\u03d0"-"\u03f5",
         "\u03f7"-"\u03fb",
         "\u0400"-"\u0481",
         "\u0483"-"\u0486",
         "\u048a"-"\u04ce",
         "\u04d0"-"\u04f5",
         "\u04f8"-"\u04f9",
         "\u0500"-"\u050f",
         "\u0531"-"\u0556",
         "\u0559",
         "\u0561"-"\u0587",
         "\u0591"-"\u05a1",
         "\u05a3"-"\u05b9",
         "\u05bb"-"\u05bd",
         "\u05bf",
         "\u05c1"-"\u05c2",
         "\u05c4",
         "\u05d0"-"\u05ea",
         "\u05f0"-"\u05f2",
         "\u0600"-"\u0603",
         "\u0610"-"\u0615",
         "\u0621"-"\u063a",
         "\u0640"-"\u0658",
         "\u0660"-"\u0669",
         "\u066e"-"\u06d3",
         "\u06d5"-"\u06dd",
         "\u06df"-"\u06e8",
         "\u06ea"-"\u06fc",
         "\u06ff",
         "\u070f"-"\u074a",
         "\u074d"-"\u074f",
         "\u0780"-"\u07b1",
         "\u0901"-"\u0939",
         "\u093c"-"\u094d",
         "\u0950"-"\u0954",
         "\u0958"-"\u0963",
         "\u0966"-"\u096f",
         "\u0981"-"\u0983",
         "\u0985"-"\u098c",
         "\u098f"-"\u0990",
         "\u0993"-"\u09a8",
         "\u09aa"-"\u09b0",
         "\u09b2",
         "\u09b6"-"\u09b9",
         "\u09bc"-"\u09c4",
         "\u09c7"-"\u09c8",
         "\u09cb"-"\u09cd",
         "\u09d7",
         "\u09dc"-"\u09dd",
         "\u09df"-"\u09e3",
         "\u09e6"-"\u09f3",
         "\u0a01"-"\u0a03",
         "\u0a05"-"\u0a0a",
         "\u0a0f"-"\u0a10",
         "\u0a13"-"\u0a28",
         "\u0a2a"-"\u0a30",
         "\u0a32"-"\u0a33",
         "\u0a35"-"\u0a36",
         "\u0a38"-"\u0a39",
         "\u0a3c",
         "\u0a3e"-"\u0a42",
         "\u0a47"-"\u0a48",
         "\u0a4b"-"\u0a4d",
         "\u0a59"-"\u0a5c",
         "\u0a5e",
         "\u0a66"-"\u0a74",
         "\u0a81"-"\u0a83",
         "\u0a85"-"\u0a8d",
         "\u0a8f"-"\u0a91",
         "\u0a93"-"\u0aa8",
         "\u0aaa"-"\u0ab0",
         "\u0ab2"-"\u0ab3",
         "\u0ab5"-"\u0ab9",
         "\u0abc"-"\u0ac5",
         "\u0ac7"-"\u0ac9",
         "\u0acb"-"\u0acd",
         "\u0ad0",
         "\u0ae0"-"\u0ae3",
         "\u0ae6"-"\u0aef",
         "\u0af1",
         "\u0b01"-"\u0b03",
         "\u0b05"-"\u0b0c",
         "\u0b0f"-"\u0b10",
         "\u0b13"-"\u0b28",
         "\u0b2a"-"\u0b30",
         "\u0b32"-"\u0b33",
         "\u0b35"-"\u0b39",
         "\u0b3c"-"\u0b43",
         "\u0b47"-"\u0b48",
         "\u0b4b"-"\u0b4d",
         "\u0b56"-"\u0b57",
         "\u0b5c"-"\u0b5d",
         "\u0b5f"-"\u0b61",
         "\u0b66"-"\u0b6f",
         "\u0b71",
         "\u0b82"-"\u0b83",
         "\u0b85"-"\u0b8a",
         "\u0b8e"-"\u0b90",
         "\u0b92"-"\u0b95",
         "\u0b99"-"\u0b9a",
         "\u0b9c",
         "\u0b9e"-"\u0b9f",
         "\u0ba3"-"\u0ba4",
         "\u0ba8"-"\u0baa",
         "\u0bae"-"\u0bb5",
         "\u0bb7"-"\u0bb9",
         "\u0bbe"-"\u0bc2",
         "\u0bc6"-"\u0bc8",
         "\u0bca"-"\u0bcd",
         "\u0bd7",
         "\u0be7"-"\u0bef",
         "\u0bf9",
         "\u0c01"-"\u0c03",
         "\u0c05"-"\u0c0c",
         "\u0c0e"-"\u0c10",
         "\u0c12"-"\u0c28",
         "\u0c2a"-"\u0c33",
         "\u0c35"-"\u0c39",
         "\u0c3e"-"\u0c44",
         "\u0c46"-"\u0c48",
         "\u0c4a"-"\u0c4d",
         "\u0c55"-"\u0c56",
         "\u0c60"-"\u0c61",
         "\u0c66"-"\u0c6f",
         "\u0c82"-"\u0c83",
         "\u0c85"-"\u0c8c",
         "\u0c8e"-"\u0c90",
         "\u0c92"-"\u0ca8",
         "\u0caa"-"\u0cb3",
         "\u0cb5"-"\u0cb9",
         "\u0cbc"-"\u0cc4",
         "\u0cc6"-"\u0cc8",
         "\u0cca"-"\u0ccd",
         "\u0cd5"-"\u0cd6",
         "\u0cde",
         "\u0ce0"-"\u0ce1",
         "\u0ce6"-"\u0cef",
         "\u0d02"-"\u0d03",
         "\u0d05"-"\u0d0c",
         "\u0d0e"-"\u0d10",
         "\u0d12"-"\u0d28",
         "\u0d2a"-"\u0d39",
         "\u0d3e"-"\u0d43",
         "\u0d46"-"\u0d48",
         "\u0d4a"-"\u0d4d",
         "\u0d57",
         "\u0d60"-"\u0d61",
         "\u0d66"-"\u0d6f",
         "\u0d82"-"\u0d83",
         "\u0d85"-"\u0d96",
         "\u0d9a"-"\u0db1",
         "\u0db3"-"\u0dbb",
         "\u0dbd",
         "\u0dc0"-"\u0dc6",
         "\u0dca",
         "\u0dcf"-"\u0dd4",
         "\u0dd6",
         "\u0dd8"-"\u0ddf",
         "\u0df2"-"\u0df3",
         "\u0e01"-"\u0e3a",
         "\u0e3f"-"\u0e4e",
         "\u0e50"-"\u0e59",
         "\u0e81"-"\u0e82",
         "\u0e84",
         "\u0e87"-"\u0e88",
         "\u0e8a",
         "\u0e8d",
         "\u0e94"-"\u0e97",
         "\u0e99"-"\u0e9f",
         "\u0ea1"-"\u0ea3",
         "\u0ea5",
         "\u0ea7",
         "\u0eaa"-"\u0eab",
         "\u0ead"-"\u0eb9",
         "\u0ebb"-"\u0ebd",
         "\u0ec0"-"\u0ec4",
         "\u0ec6",
         "\u0ec8"-"\u0ecd",
         "\u0ed0"-"\u0ed9",
         "\u0edc"-"\u0edd",
         "\u0f00",
         "\u0f18"-"\u0f19",
         "\u0f20"-"\u0f29",
         "\u0f35",
         "\u0f37",
         "\u0f39",
         "\u0f3e"-"\u0f47",
         "\u0f49"-"\u0f6a",
         "\u0f71"-"\u0f84",
         "\u0f86"-"\u0f8b",
         "\u0f90"-"\u0f97",
         "\u0f99"-"\u0fbc",
         "\u0fc6",
         "\u1000"-"\u1021",
         "\u1023"-"\u1027",
         "\u1029"-"\u102a",
         "\u102c"-"\u1032",
         "\u1036"-"\u1039",
         "\u1040"-"\u1049",
         "\u1050"-"\u1059",
         "\u10a0"-"\u10c5",
         "\u10d0"-"\u10f8",
         "\u1100"-"\u1159",
         "\u115f"-"\u11a2",
         "\u11a8"-"\u11f9",
         "\u1200"-"\u1206",
         "\u1208"-"\u1246",
         "\u1248",
         "\u124a"-"\u124d",
         "\u1250"-"\u1256",
         "\u1258",
         "\u125a"-"\u125d",
         "\u1260"-"\u1286",
         "\u1288",
         "\u128a"-"\u128d",
         "\u1290"-"\u12ae",
         "\u12b0",
         "\u12b2"-"\u12b5",
         "\u12b8"-"\u12be",
         "\u12c0",
         "\u12c2"-"\u12c5",
         "\u12c8"-"\u12ce",
         "\u12d0"-"\u12d6",
         "\u12d8"-"\u12ee",
         "\u12f0"-"\u130e",
         "\u1310",
         "\u1312"-"\u1315",
         "\u1318"-"\u131e",
         "\u1320"-"\u1346",
         "\u1348"-"\u135a",
         "\u1369"-"\u1371",
         "\u13a0"-"\u13f4",
         "\u1401"-"\u166c",
         "\u166f"-"\u1676",
         "\u1681"-"\u169a",
         "\u16a0"-"\u16ea",
         "\u16ee"-"\u16f0",
         "\u1700"-"\u170c",
         "\u170e"-"\u1714",
         "\u1720"-"\u1734",
         "\u1740"-"\u1753",
         "\u1760"-"\u176c",
         "\u176e"-"\u1770",
         "\u1772"-"\u1773",
         "\u1780"-"\u17d3",
         "\u17d7",
         "\u17db"-"\u17dd",
         "\u17e0"-"\u17e9",
         "\u180b"-"\u180d",
         "\u1810"-"\u1819",
         "\u1820"-"\u1877",
         "\u1880"-"\u18a9",
         "\u1900"-"\u191c",
         "\u1920"-"\u192b",
         "\u1930"-"\u193b",
         "\u1946"-"\u196d",
         "\u1970"-"\u1974",
         "\u1d00"-"\u1d6b",
         "\u1e00"-"\u1e9b",
         "\u1ea0"-"\u1ef9",
         "\u1f00"-"\u1f15",
         "\u1f18"-"\u1f1d",
         "\u1f20"-"\u1f45",
         "\u1f48"-"\u1f4d",
         "\u1f50"-"\u1f57",
         "\u1f59",
         "\u1f5b",
         "\u1f5d",
         "\u1f5f"-"\u1f7d",
         "\u1f80"-"\u1fb4",
         "\u1fb6"-"\u1fbc",
         "\u1fbe",
         "\u1fc2"-"\u1fc4",
         "\u1fc6"-"\u1fcc",
         "\u1fd0"-"\u1fd3",
         "\u1fd6"-"\u1fdb",
         "\u1fe0"-"\u1fec",
         "\u1ff2"-"\u1ff4",
         "\u1ff6"-"\u1ffc",
         "\u200c"-"\u200f",
         "\u202a"-"\u202e",
         "\u203f"-"\u2040",
         "\u2054",
         "\u2060"-"\u2063",
         "\u206a"-"\u206f",
         "\u2071",
         "\u207f",
         "\u20a0"-"\u20b1",
         "\u20d0"-"\u20dc",
         "\u20e1",
         "\u20e5"-"\u20ea",
         "\u2102",
         "\u2107",
         "\u210a"-"\u2113",
         "\u2115",
         "\u2119"-"\u211d",
         "\u2124",
         "\u2126",
         "\u2128",
         "\u212a"-"\u212d",
         "\u212f"-"\u2131",
         "\u2133"-"\u2139",
         "\u213d"-"\u213f",
         "\u2145"-"\u2149",
         "\u2160"-"\u2183",
         "\u3005"-"\u3007",
         "\u3021"-"\u302f",
         "\u3031"-"\u3035",
         "\u3038"-"\u303c",
         "\u3041"-"\u3096",
         "\u3099"-"\u309a",
         "\u309d"-"\u309f",
         "\u30a1"-"\u30ff",
         "\u3105"-"\u312c",
         "\u3131"-"\u318e",
         "\u31a0"-"\u31b7",
         "\u31f0"-"\u31ff",
         "\u3400"-"\u4db5",
         "\u4e00"-"\u9fa5",
         "\ua000"-"\ua48c",
         "\uac00"-"\ud7a3",
         "\ud801", //for supplementary characters suport
         "\ud802", //for supplementary characters suport
         "\ud834", //for supplementary characters suport
         "\udc00", //for supplementary characters suport
         "\udc01", //for supplementary characters suport
         "\udd7b", //for supplementary characters suport
         "\uf900"-"\ufa2d",
         "\ufa30"-"\ufa6a",
         "\ufb00"-"\ufb06",
         "\ufb13"-"\ufb17",
         "\ufb1d"-"\ufb28",
         "\ufb2a"-"\ufb36",
         "\ufb38"-"\ufb3c",
         "\ufb3e",
         "\ufb40"-"\ufb41",
         "\ufb43"-"\ufb44",
         "\ufb46"-"\ufbb1",
         "\ufbd3"-"\ufd3d",
         "\ufd50"-"\ufd8f",
         "\ufd92"-"\ufdc7",
         "\ufdf0"-"\ufdfc",
         "\ufe00"-"\ufe0f",
         "\ufe20"-"\ufe23",
         "\ufe33"-"\ufe34",
         "\ufe4d"-"\ufe4f",
         "\ufe69",
         "\ufe70"-"\ufe74",
         "\ufe76"-"\ufefc",
         "\ufeff",
         "\uff04",
         "\uff10"-"\uff19",
         "\uff21"-"\uff3a",
         "\uff3f",
         "\uff41"-"\uff5a",
         "\uff65"-"\uffbe",
         "\uffc2"-"\uffc7",
         "\uffca"-"\uffcf",
         "\uffd2"-"\uffd7",
         "\uffda"-"\uffdc",
         "\uffe0"-"\uffe1",
         "\uffe5"-"\uffe6",
         "\ufff9"-"\ufffb"
      ]
  >
}

/**********************************************************************************************************************
 * Quote variables
 **********************************************************************************************************************/

BUTree<SNodeList> NodeListVar():{}
{
  <NODE_LIST_VARIABLE>
  { return makeVar(); }
}

BUTree<SName> NodeVar():{}
{
  <NODE_VARIABLE>
  { return makeVar(); }
}

/*****************************************
 * THE JAVA LANGUAGE GRAMMAR STARTS HERE *
 *****************************************/

BUTree<SCompilationUnit> CompilationUnit():
{
	BUTree<SPackageDecl> packageDecl = null;
	BUTree<SNodeList> imports;
	BUTree<SNodeList> types;
	BUTree<SCompilationUnit> compilationUnit;
}
{
  { run(); }

  [ LOOKAHEAD(PackageDecl()) packageDecl = PackageDecl() ]
  imports = ImportDecls()
  types = TypeDecls()

  { compilationUnit = dress(SCompilationUnit.make(packageDecl, imports, types)); }

  Epilog()
  { return dressWithPrologAndEpilog(compilationUnit); }
}

void Epilog(): {}
{
  (<EOF> | "\u001A" /** ctrl+z char **/)
}


/**********************************************************************************************************************
 * Declarations
 **********************************************************************************************************************/

BUTree<SPackageDecl> PackageDecl():
{
	BUTree<SNodeList> annotations = null;
	BUTree<SQualifiedName> name;
}
{
  { run(); }

  annotations = Annotations() "package" name = QualifiedName() ";"

  { return dress(SPackageDecl.make(annotations, name)); }
}

BUTree<SNodeList> ImportDecls():
{
	BUTree<SNodeList> imports = emptyList();
	BUTree<SImportDecl> importDecl = null;
}
{
  ( importDecl = ImportDecl() { imports = append(imports, importDecl); } )*
  { return imports; }
}

BUTree<SImportDecl> ImportDecl():
{
	BUTree<SQualifiedName> name;
	boolean isStatic = false;
	boolean isAsterisk = false;
}
{
  { run(); }
  "import" [ "static" { isStatic = true; } ] name = QualifiedName() [ "." "*" { isAsterisk = true; } ] ";"
  { return dress(SImportDecl.make(name, isStatic, isAsterisk)); }
}

BUTree<SNodeList> TypeDecls():
{
	BUTree<SNodeList> types = emptyList();
	BUTree<? extends STypeDecl> typeDecl = null;
}
{
  ( typeDecl = TypeDecl() { types = append(types, typeDecl); } )*
  { return types; }
}

/*
 * Modifiers. We match all modifiers in a single rule to reduce the chances of
 * syntax errors for simple modifier mistakes. It will also enable us to give
 * better error messages.
 */

BUTree<SNodeList> Modifiers():
{
	BUTree<SNodeList> modifiers = emptyList();
	BUTree<? extends SAnnotationExpr> ann;
}
{
  (
    LOOKAHEAD(2)
    (
      "public" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Public)); }
    |
      "protected" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Protected)); }
    |
      "private" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Private)); }
    |
      "abstract" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Abstract)); }
    |
      "default" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Default)); }
    |
      "static" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Static)); }
    |
      "final" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Final)); }
    |
      "transient" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Transient)); }
    |
      "volatile" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Volatile)); }
    |
      "synchronized" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Synchronized)); }
    |
      "native" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Native)); }
    |
      "strictfp" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.StrictFP)); }
    |
      ann = Annotation() { modifiers = append(modifiers, ann); }
    )
  )*

  { return modifiers; }
}

BUTree<SNodeList> ModifiersNoDefault():
{
	BUTree<SNodeList> modifiers = emptyList();
	BUTree<? extends SAnnotationExpr> ann;
}
{
  (
    LOOKAHEAD(2)
    (
      "public" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Public)); }
    |
      "protected" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Protected)); }
    |
      "private" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Private)); }
    |
      "abstract" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Abstract)); }
    |
      "static" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Static)); }
    |
      "final" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Final)); }
    |
      "transient" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Transient)); }
    |
      "volatile" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Volatile)); }
    |
      "synchronized" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Synchronized)); }
    |
      "native" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.Native)); }
    |
      "strictfp" { modifiers = append(modifiers, SModifier.make(ModifierKeyword.StrictFP)); }
    |
      ann = Annotation() { modifiers = append(modifiers, ann); }
    )
  )*

  { return modifiers; }
}

/*
 * Decl syntax follows.
 */
BUTree<? extends STypeDecl> TypeDecl():
{
	BUTree<SNodeList> modifiers;
	BUTree<? extends STypeDecl> ret;
}
{
  { run(); }

  (
    ";" { ret = dress(SEmptyTypeDecl.make()); }
  |
    modifiers = Modifiers()
    (
      ret = ClassOrInterfaceDecl(modifiers)
    |
      ret = EnumDecl(modifiers)
    |
      ret = AnnotationTypeDecl(modifiers)
    )
  )

  { return ret; }
}


BUTree<? extends STypeDecl> ClassOrInterfaceDecl(BUTree<SNodeList> modifiers):
{
	TypeKind typeKind;
	BUTree<SName> name;
	BUTree<SNodeList> typeParams = null;
	BUTree<SQualifiedType> superClassType = null;
	BUTree<SNodeList> extendsClause = null;
	BUTree<SNodeList> implementsClause = null;
	BUTree<SNodeList> members;
}
{
  // Run is started before modifiers

  (
    "class" { typeKind = TypeKind.Class; }
    name = Name()
    [ typeParams = TypeParameters() ]
    [ "extends" superClassType = AnnotatedQualifiedType() ]
    [ implementsClause = ImplementsList(typeKind) ]
  |
    "interface" { typeKind = TypeKind.Interface; }
    name = Name()
    [ typeParams = TypeParameters() ]
    [ extendsClause = ExtendsList() ]
  )
  members = ClassOrInterfaceBody(typeKind)

  {
    if (typeKind == TypeKind.Interface)
      return dress(SInterfaceDecl.make(modifiers, name, ensureNotNull(typeParams), ensureNotNull(extendsClause), members));
    else {
      return dress(SClassDecl.make(modifiers, name, ensureNotNull(typeParams), optionOf(superClassType), ensureNotNull(implementsClause), members));
    }
  }
}

BUTree<SNodeList> ExtendsList():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<SQualifiedType> cit;
	BUTree<SNodeList> annotations = null;
}
{
  "extends"
  (
    cit = AnnotatedQualifiedType() { ret = append(ret, cit); }
    ( "," cit = AnnotatedQualifiedType() { ret = append(ret, cit); } )*
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  )
  { return ret; }
}

BUTree<SNodeList> ImplementsList(TypeKind typeKind):
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<SQualifiedType> cit;
	BUTree<SNodeList> annotations = null;
}
{
  "implements"
  (
    cit = AnnotatedQualifiedType() { ret = append(ret, cit); }
    ( "," cit = AnnotatedQualifiedType() { ret = append(ret, cit); } )*
    {
      if (typeKind == TypeKind.Interface)
        throwParseException(token, "An interface cannot implement other interfaces");
    }
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  )
  { return ret; }
}

BUTree<? extends STypeDecl> EnumDecl(BUTree<SNodeList> modifiers):
{
	BUTree<SName> name;
	BUTree<SNodeList> implementsClause = emptyList();
	BUTree<SEnumConstantDecl> entry;
	BUTree<SNodeList> constants = emptyList();
	boolean trailingComma = false;
	BUTree<SNodeList> members = null;
}
{
  // Run is started before modifiers

  "enum" name = Name() [ implementsClause = ImplementsList(TypeKind.Enum) ]
  "{"
  [
    entry = EnumConstantDecl() { constants = append(constants, entry); }
    ( LOOKAHEAD(2) "," entry = EnumConstantDecl() { constants = append(constants, entry); } )*
  |
    LOOKAHEAD({quotesMode}) constants = NodeListVar()
  ]
  [ "," { trailingComma = true; } ]
  [ ";" members = ClassOrInterfaceBodyDecls(TypeKind.Enum) ]
  "}"

  { return dress(SEnumDecl.make(modifiers, name, implementsClause, constants, trailingComma, ensureNotNull(members))); }
}

BUTree<SEnumConstantDecl> EnumConstantDecl():
{
	BUTree<SNodeList> modifiers = null;
	BUTree<SName> name;
	BUTree<SNodeList> args = null;
	BUTree<SNodeList> classBody = null;
}
{
  { run(); }

  modifiers = Modifiers() name = Name()
  [ args = Arguments() ] [ classBody = ClassOrInterfaceBody(TypeKind.Class) ]

  { return dress(SEnumConstantDecl.make(modifiers, name, optionOf(args), optionOf(classBody))); }
}

BUTree<SAnnotationDecl> AnnotationTypeDecl(BUTree<SNodeList> modifiers):
{
	BUTree<SName> name;
	BUTree<SNodeList> members;
}
{
  // Run is started before modifiers

  "@" "interface" name = Name()
  members = AnnotationTypeBody()

  { return dress(SAnnotationDecl.make(modifiers, name, members)); }
}

// TODO Merge with ClassOrInterfaceBody production and do later checks programmaticaly
BUTree<SNodeList> AnnotationTypeBody():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SMemberDecl> member;
}
{
  "{"
  [
    ( member = AnnotationTypeBodyDecl() { ret = append(ret, member); } )+
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  ]
  "}"
  { return ret; }
}

// TODO Merge with ClassOrInterfaceDecl production and do later checks programmaticaly
BUTree<? extends SMemberDecl> AnnotationTypeBodyDecl():
{
	BUTree<SNodeList> modifiers;
	BUTree<? extends SMemberDecl> ret;
}
{
  { run(); }

  (
	";" { ret = dress(SEmptyTypeDecl.make()); }
  |
    modifiers = Modifiers()
    (
      LOOKAHEAD(Type() Name() "(")
      ret = AnnotationTypeMemberDecl(modifiers)
    |
      ret = ClassOrInterfaceDecl(modifiers)
    |
      ret = EnumDecl(modifiers)
    |
      ret = AnnotationTypeDecl(modifiers)
    |
      ret = FieldDecl(modifiers)
    )
  )

  { return ret; }
}

// TODO Merge with MethodDecl production and do later checks programmaticaly
BUTree<SAnnotationMemberDecl> AnnotationTypeMemberDecl(BUTree<SNodeList> modifiers):
{
	BUTree<? extends SType> type;
	BUTree<SName> name;
	BUTree<SNodeList> dims;
	BUTree<SNodeOption> defaultVal = none();
	BUTree<? extends SExpr> val = null;
}
{
  // Run is started before modifiers

  type = Type(null) name = Name() "(" ")" dims = ArrayDims()
  [
    "default" val = MemberValue()
    { defaultVal = optionOf(val); }
  ]
  ";"

  { return dress(SAnnotationMemberDecl.make(modifiers, type, name, dims, defaultVal)); }
}

BUTree<SNodeList> TypeParameters():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<STypeParameter> tp;
}
{
  "<"
  (
    tp = TypeParameter() { ret = append(ret, tp); }
    ( "," tp = TypeParameter() { ret = append(ret, tp); } )*
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  )
  ">"

  { return ret; }
}

BUTree<STypeParameter> TypeParameter():
{
	BUTree<SNodeList> annotations = null;
	BUTree<SName> name;
	BUTree<SNodeList> typeBounds = null;
}
{
  { run(); }
  annotations = Annotations() name = Name() [ typeBounds = TypeBounds() ]
  { return dress(STypeParameter.make(annotations, name, ensureNotNull(typeBounds))); }
}

BUTree<SNodeList> TypeBounds():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<SQualifiedType> cit;
	BUTree<SNodeList> annotations = null;
}
{
  "extends"
  (
    cit = AnnotatedQualifiedType() { ret = append(ret, cit); }
    ( "&" cit = AnnotatedQualifiedType() { ret = append(ret, cit); } )*
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  )
  { return ret; }
}

BUTree<SNodeList> ClassOrInterfaceBody(TypeKind typeKind):
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SMemberDecl> member;
}
{
  "{"
  ret = ClassOrInterfaceBodyDecls(typeKind)
  "}"
  { return ret; }
}

BUTree<SNodeList> ClassOrInterfaceBodyDecls(TypeKind typeKind):
{
	BUTree<? extends SMemberDecl> member;
	BUTree<SNodeList> ret = emptyList();
}
{
  [
    ( member = ClassOrInterfaceBodyDecl(typeKind) { ret = append(ret, member); } )+
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  ]
  { return ret; }
}

BUTree<? extends SMemberDecl> ClassOrInterfaceBodyDecl(TypeKind typeKind):
{
	BUTree<SNodeList> modifiers;
	BUTree<? extends SMemberDecl> ret;
}
{
  { run(); }

  (
    ";" { ret = dress(SEmptyMemberDecl.make()); }
  |
    modifiers = Modifiers()
    {
      if (modifiers != null && contains(modifiers, SModifier.make(ModifierKeyword.Default)) && typeKind != TypeKind.Interface)
        throwParseException(token, "Only interfaces can have default members");
    }
    (
      LOOKAHEAD( "{" )
      ret = InitializerDecl(modifiers)
      {
        if (typeKind == TypeKind.Interface)
	      throwParseException(token, "An interface cannot have initializers");
	  }
    |
      ret = ClassOrInterfaceDecl(modifiers)
    |
      ret = EnumDecl(modifiers)
    |
      ret = AnnotationTypeDecl(modifiers)
    |
      LOOKAHEAD( [ TypeParameters() ] Name() "(" )
      ret = ConstructorDecl(modifiers)
      {
        if (typeKind == TypeKind.Interface)
	      throwParseException(token, "An interface cannot have constructors");
	  }
    |
      LOOKAHEAD( Type() Name() ( "[" "]" )* ( "," | "=" | ";" ) )
      ret = FieldDecl(modifiers)
    |
      ret = MethodDecl(modifiers)
    )
  )

  { return ret; }
}

BUTree<SFieldDecl> FieldDecl(BUTree<SNodeList> modifiers):
{
	BUTree<? extends SType> type;
	BUTree<SNodeList> variables = emptyList();
	BUTree<SVariableDeclarator> val;
}
{
  // Run is started before modifiers

  type = Type(null) variables = VariableDeclarators() ";"

  { return dress(SFieldDecl.make(modifiers, type, variables)); }
}

BUTree<SLocalVariableDecl> VariableDecl(BUTree<SNodeList> modifiers):
{
	BUTree<? extends SType> type;
	BUTree<SNodeList> variables = emptyList();
}
{
  // Run is started before modifiers

  type = Type(null) variables = VariableDeclarators()

  { return dress(SLocalVariableDecl.make(modifiers, type, variables)); }
}

BUTree<SNodeList> VariableDeclarators():
{
	BUTree<SNodeList> variables = emptyList();
	BUTree<SVariableDeclarator> val;
}
{
        val = VariableDeclarator() { variables = append(variables, val); }
  ( "," val = VariableDeclarator() { variables = append(variables, val); } )*

  { return variables; }
}

BUTree<SVariableDeclarator> VariableDeclarator():
{
	BUTree<SVariableDeclaratorId> id;
	BUTree<SNodeOption> init = none();
	BUTree<? extends SExpr> initExpr = null;
}
{
  { run(); }

  id = VariableDeclaratorId()
  [
    "=" initExpr = VariableInitializer()
    { init = optionOf(initExpr); }
  ]

  { return dress(SVariableDeclarator.make(id, init)); }
}

BUTree<SVariableDeclaratorId> VariableDeclaratorId():
{
	BUTree<SName> name;
	BUTree<SNodeList> arrayDims;
}
{
  { run(); }

  name = Name() arrayDims = ArrayDims()

  { return dress(SVariableDeclaratorId.make(name, arrayDims)); }
}

BUTree<SNodeList> ArrayDims():
{
	BUTree<SNodeList> arrayDims = emptyList();
	BUTree<SNodeList> annotations;
}
{
  (
    LOOKAHEAD( Annotations() "[" "]" )
    { run(); }
    annotations = Annotations() "[" "]"
    { arrayDims = append(arrayDims, dress(SArrayDim.make(annotations))); }
  )*
  { return arrayDims; }
}

BUTree<? extends SExpr> VariableInitializer():
{
	BUTree<? extends SExpr> ret;
}
{
  ( ret = ArrayInitializer() | ret = Expression() )
  { return ret; }
}

BUTree<SArrayInitializerExpr> ArrayInitializer():
{
	BUTree<SNodeList> values = emptyList();
	BUTree<? extends SExpr> val;
	boolean trailingComma = false;
}
{
  { run(); }
  "{"
  [
    val = VariableInitializer() { values = append(values, val); }
    ( LOOKAHEAD(2) "," val = VariableInitializer() { values = append(values, val); } )*
  ]
  [ "," { trailingComma = true; } ]
  "}"

  { return dress(SArrayInitializerExpr.make(values, trailingComma)); }
}

BUTree<SMethodDecl> MethodDecl(BUTree<SNodeList> modifiers):
{
	BUTree<SNodeList> typeParameters = null;
	BUTree<? extends SType> type;
	BUTree<SName> name;
	BUTree<SNodeList> parameters;
	BUTree<SNodeList> arrayDims;
	BUTree<SNodeList> throwsClause = null;
	BUTree<SBlockStmt> block = null;
}
{
  // Run is started before modifiers

  [ typeParameters = TypeParameters() ]
  type = ResultType()
  name = Name() parameters = FormalParameters() arrayDims = ArrayDims()
  [ throwsClause = ThrowsClause() ]
  (
    block = Block()
  |
    ";"
    {
      if(modifiers != null && contains(modifiers, SModifier.make(ModifierKeyword.Default)))
	    throwParseException(token, "default methods must have a body");
	}
  )

  { return dress(SMethodDecl.make(modifiers, ensureNotNull(typeParameters), type, name, parameters, arrayDims, ensureNotNull(throwsClause), optionOf(block))); }
}

BUTree<SNodeList> FormalParameters():
{
	BUTree<SNodeList> ret = null;
	BUTree<SFormalParameter> par;
}
{
  "("
  [
    ret = FormalParameterList()
  ]
  ")"
  { return ensureNotNull(ret); }
}

BUTree<SNodeList> FormalParameterList():
{
	BUTree<SNodeList> ret = null;
	BUTree<SFormalParameter> par;
}
{
  (
    par = FormalParameter() { ret = append(ret, par); }
    ( "," par = FormalParameter() { ret = append(ret, par); } )*
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  )
  { return ret; }
}

BUTree<SFormalParameter> FormalParameter():
{
	BUTree<SNodeList> modifiers;
	BUTree<? extends SType> type;
	boolean isVarArg = false;
	BUTree<SVariableDeclaratorId> id;
}
{
  { run(); }

  modifiers = Modifiers() type = Type(null) [ "..." { isVarArg = true; } ] id = VariableDeclaratorId()

  { return dress(SFormalParameter.make(modifiers, type, isVarArg, id)); }
}


BUTree<SNodeList> ThrowsClause():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<SQualifiedType> cit;
}
{
  "throws" cit = AnnotatedQualifiedType() { ret = append(ret, cit); }
  ( "," cit = AnnotatedQualifiedType() { ret = append(ret, cit); } )*

  { return ret; }
}

BUTree<SConstructorDecl> ConstructorDecl(BUTree<SNodeList> modifiers):
{
	BUTree<SNodeList> typeParameters = null;
	BUTree<SName> name;
	BUTree<SNodeList> parameters;
	BUTree<SNodeList> throwsClause = null;
	BUTree<SExplicitConstructorInvocationStmt> exConsInv = null;
	BUTree<SBlockStmt> block;
	BUTree<SNodeList> stmts = emptyList();
	BUTree<? extends SStmt> stmt;
}
{
  // Run is started before modifiers

  [ typeParameters = TypeParameters() ]
  name = Name() parameters = FormalParameters()
  [ throwsClause = ThrowsClause() ]

  { run(); }
  "{"
  [
    LOOKAHEAD(2)
    (
      LOOKAHEAD(ExplicitConstructorInvocation()) stmt = ExplicitConstructorInvocation() { stmts = append(stmts, stmt); }
    |
      LOOKAHEAD(2) stmt = BlockStatement() { stmts = append(stmts, stmt); }
    )
    ( LOOKAHEAD(2) stmt = BlockStatement() { stmts = append(stmts, stmt); } )*
  |
    LOOKAHEAD({quotesMode}) stmts = NodeListVar()
  ]
  "}"
  { block = dress(SBlockStmt.make(stmts)); }

  { return dress(SConstructorDecl.make(modifiers, ensureNotNull(typeParameters), name, parameters, ensureNotNull(throwsClause), block)); }
}

BUTree<SExplicitConstructorInvocationStmt> ExplicitConstructorInvocation():
{
	boolean isThis = false;
	BUTree<SNodeList> args;
	BUTree<? extends SExpr> expr = null;
	BUTree<SNodeList> typeArgs = null;
}
{
  { run(); }

  (
    LOOKAHEAD([ TypeArguments() ] "this" "(")
    [ typeArgs = TypeArguments() ]
    "this" { isThis = true; }
    args = Arguments() ";"
  |
    [
      LOOKAHEAD( PrimaryExpressionWithoutSuperSuffix() "." )
      expr = PrimaryExpressionWithoutSuperSuffix() "."
    ]
    [ typeArgs = TypeArguments() ]
    "super"
    args = Arguments() ";"
  )

  { return dress(SExplicitConstructorInvocationStmt.make(ensureNotNull(typeArgs), isThis, optionOf(expr), args)); }
}

BUTree<SNodeList> Statements():
{
	BUTree<SNodeList> ret = null;
	BUTree<? extends SStmt> stmt;
}
{
  [
    LOOKAHEAD(2)
    (
      ( stmt = BlockStatement() { ret = append(ret, stmt); } )+
    |
      LOOKAHEAD({quotesMode}) ret = NodeListVar()
    )
  ]
  { return ret; }
}

BUTree<SInitializerDecl> InitializerDecl(BUTree<SNodeList> modifiers):
{
	BUTree<SBlockStmt> block;
}
{
  // Run is started before modifiers

  block = Block()

  { return dress(SInitializerDecl.make(modifiers, block)); }
}


/**********************************************************************************************************************
 * Types
 **********************************************************************************************************************/

BUTree<? extends SType> Type(BUTree<SNodeList> annotations):
{
//	BUTree<? extends SType> ret;
	BUTree<? extends SType> primitiveType = null;
	BUTree<? extends SReferenceType> type = null;
	BUTree<SNodeList> arrayDims;
}
{
  // Run may be started before annotations

  (
    primitiveType = PrimitiveType(annotations)
    [
      LOOKAHEAD( Annotations() "[" )
      { lateRun(); }
      arrayDims = ArrayDimsMandatory()
      { type = dress(SArrayType.make(primitiveType, arrayDims)); }
    ]
  |
    type = QualifiedType(annotations)
    [
      LOOKAHEAD( Annotations() "[" )
      { lateRun(); }
      arrayDims = ArrayDimsMandatory()
      { type = dress(SArrayType.make(type, arrayDims)); }
    ]
  )

  { return type == null ? primitiveType : type; }
}

BUTree<? extends SReferenceType> ReferenceType(BUTree<SNodeList> annotations):
{
	BUTree<? extends SType> primitiveType;
	BUTree<? extends SReferenceType> type;
	BUTree<SNodeList> arrayDims;
}
{
  // Run may be started before annotations

  (
    primitiveType = PrimitiveType(annotations)
    { lateRun(); }
    arrayDims = ArrayDimsMandatory()
    { type = dress(SArrayType.make(primitiveType, arrayDims)); }
  |
    type = QualifiedType(annotations)
    [
      LOOKAHEAD( Annotations() "[" )
      { lateRun(); }
      arrayDims = ArrayDimsMandatory()
      { type = dress(SArrayType.make(type, arrayDims)); }
    ]
  )

  { return type; }
}

BUTree<SQualifiedType> QualifiedType(BUTree<SNodeList> annotations):
{
    BUTree<SNodeOption> scope = none();
	BUTree<SQualifiedType> ret;
	BUTree<SName> name;
	BUTree<SNodeList> typeArgs = null;
}
{
  // Run may be started before annotations
  {
    if (annotations == null) {
      run();
      annotations = emptyList();
    }
  }

  name = Name()
  [ LOOKAHEAD(2) typeArgs = TypeArgumentsOrDiamond() ]
  { ret = dress(SQualifiedType.make(annotations, scope, name, optionOf(typeArgs))); }
  (
    LOOKAHEAD(2)
    "." { scope = optionOf(ret); }

    { lateRun(); }
    annotations = Annotations() name = Name()
    [ LOOKAHEAD(2) typeArgs = TypeArgumentsOrDiamond() ]
    { ret = dress(SQualifiedType.make(annotations, scope, name, optionOf(typeArgs))); }
  )*

  { return ret; }
}

BUTree<SNodeList> TypeArguments():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SType> type;
}
{
  "<"
  [ ret = TypeArgumentList() ]
  ">"
  { return ret; }
}


BUTree<SNodeList> TypeArgumentsOrDiamond():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SType> type;
}
{
  "<"
  [ ret = TypeArgumentList() ]
  ">"
  { return ret; }
}

BUTree<SNodeList> TypeArgumentList():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SType> type;
}
{
  type = TypeArgument() { ret = append(ret, type); }
  ( "," type = TypeArgument() { ret = append(ret, type); } )*
  { return ret; }
|
  LOOKAHEAD({quotesMode})
  <NODE_LIST_VARIABLE> { return makeVar(); }
}

BUTree<? extends SType> TypeArgument():
{
	BUTree<? extends SType> ret;
	BUTree<SNodeList> annotations = null;
}
{
  { run(); }
  annotations = Annotations()
  (
    ret = ReferenceType(annotations)
  |
    ret = Wildcard(annotations)
  )
  { return ret; }
}

BUTree<SWildcardType> Wildcard(BUTree<SNodeList> annotations):
{
	BUTree<? extends SReferenceType> ext = null;
	BUTree<? extends SReferenceType> sup = null;
	BUTree<SNodeList> boundAnnotations = null;
}
{
  // Run may be started before annotations
  {
    if (annotations == null) {
      run();
      annotations = emptyList();
    }
  }

  "?"
  [ "extends" { run(); } boundAnnotations = Annotations() ext = ReferenceType(boundAnnotations)
  | "super" { run(); } boundAnnotations = Annotations() sup = ReferenceType(boundAnnotations) ]
  { return dress(SWildcardType.make(annotations, optionOf(ext), optionOf(sup))); }
}

BUTree<SPrimitiveType> PrimitiveType(BUTree<SNodeList> annotations):
{
	Primitive primitive;
}
{
  // Run may be started before annotations
  {
    if (annotations == null) {
      run();
      annotations = emptyList();
    }
  }

  ( "boolean" { primitive = Primitive.Boolean; }
  | "char"    { primitive = Primitive.Char; }
  | "byte"    { primitive = Primitive.Byte; }
  | "short"   { primitive = Primitive.Short; }
  | "int"     { primitive = Primitive.Int; }
  | "long"    { primitive = Primitive.Long; }
  | "float"   { primitive = Primitive.Float; }
  | "double"  { primitive = Primitive.Double; } )
  { return dress(SPrimitiveType.make(annotations, primitive)); }
}

BUTree<? extends SType> ResultType():
{
	BUTree<? extends SType> ret;
}
{
  (
    { run(); } "void" { ret = dress(SVoidType.make()); }
  |
    ret = Type(null)
  )
  { return ret; }
}

BUTree<SQualifiedType> AnnotatedQualifiedType():
{
    BUTree<SNodeList> annotations;
	BUTree<SQualifiedType> ret;
}
{
  { run(); }
  annotations = Annotations() ret = QualifiedType(annotations)
  { return ret; }
}

/**********************************************************************************************************************
 * Names
 **********************************************************************************************************************/

BUTree<SQualifiedName> QualifiedName():
{
    BUTree<SNodeOption> qualifier = none();
	BUTree<SQualifiedName> ret = null;
	BUTree<SName> name;
}
{
  { run(); } name = Name() { ret = dress(SQualifiedName.make(qualifier, name)); }
  (
    LOOKAHEAD(2)
    { lateRun(); }
    "." { qualifier = optionOf(ret); }
    name = Name() { ret = dress(SQualifiedName.make(qualifier, name)); }
  )*
  { return ret; }
}

BUTree<SName> Name():
{
    BUTree<SName> name;
}
{
  (
    { run(); }
    <IDENTIFIER>
    { name = dress(SName.make(token.image)); }
  |
    LOOKAHEAD({quotesMode}) name = NodeVar()
  )
  { return name; }
}

/**********************************************************************************************************************
 * Expressions
 **********************************************************************************************************************/

BUTree<? extends SExpr> Expression():
// FIXME
// This expansion has been written this way instead of :
//   Assignment() | ConditionalExpression()
// for performance reasons.
// However, it is a weakening of the grammar for it allows the LHS of
// assignments to be any conditional Expr whereas it can only be
// a primary Expr.  Consider adding a semantic predicate to work
// around this.
{
	BUTree<? extends SExpr> ret;
	AssignOp op;
	BUTree<? extends SExpr> value;
	BUTree<SNodeList> params;
}
{
  (
    LOOKAHEAD( Name() "->" )
    { run(); } ret = Name() "->" ret = LambdaBody(singletonList(makeFormalParameter((BUTree<SName>) ret)), false)
  |
    LOOKAHEAD( "(" ")" "->" )
    { run(); } "(" ")" "->" ret = LambdaBody(emptyList(), true)
  |
    LOOKAHEAD( "(" Name() ")" "->" )
    { run(); } "(" ret = Name() ")" "->" ret = LambdaBody(singletonList(makeFormalParameter((BUTree<SName>) ret)), true)
  |
    LOOKAHEAD( "(" Name() "," )
    { run(); } "(" params = InferredFormalParameterList() ")" "->" ret = LambdaBody(params, true)
  |
    ret = ConditionalExpression()
    [
      LOOKAHEAD(2)
      { lateRun(); } op = AssignmentOperator() value = Expression()
      { ret = dress(SAssignExpr.make(ret, op, value)); }
    ]
  )
  { return ret; }
}

BUTree<SLambdaExpr> LambdaBody(BUTree<SNodeList> parameters, boolean parenthesis):
{
	BUTree<SBlockStmt> block;
	BUTree<? extends SExpr> expr;
	BUTree<SLambdaExpr> ret;
}
{
  // Run was started before parameters
  (
    expr = Expression() { ret = dress(SLambdaExpr.make(parameters, parenthesis, left(expr))); }
  |
    block = Block() { ret = dress(SLambdaExpr.make(parameters, parenthesis, right(block))); }
  )
  { return ret; }
}

BUTree<SNodeList> InferredFormalParameterList():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<SFormalParameter> param;
}
{
  param = InferredFormalParameter() { ret = append(ret, param); }
  ( "," param = InferredFormalParameter() { ret = append(ret, param); } )*
  { return ret; }
}

BUTree<SFormalParameter> InferredFormalParameter():
{
	BUTree<SName> name;
}
{
  name = Name()
  { return makeFormalParameter(name); }
}

JAVACODE
BUTree<SFormalParameter> makeFormalParameter(BUTree<SName> name) {
  return SFormalParameter.make(emptyList(), SUnknownType.make(), false,
    SVariableDeclaratorId.make(name, emptyList())
  );
}

AssignOp AssignmentOperator():
{
	AssignOp ret;
}
{
  ( "="    { ret = AssignOp.Normal; }
  | "*="   { ret = AssignOp.Times; }
  | "/="   { ret = AssignOp.Divide; }
  | "%="   { ret = AssignOp.Remainder; }
  | "+="   { ret = AssignOp.Plus; }
  | "-="   { ret = AssignOp.Minus; }
  | "<<="  { ret = AssignOp.LeftShift; }
  | ">>="  { ret = AssignOp.RightSignedShift; }
  | ">>>=" { ret = AssignOp.RightUnsignedShift; }
  | "&="   { ret = AssignOp.And; }
  | "^="   { ret = AssignOp.XOr; }
  | "|="   { ret = AssignOp.Or; } )
  { return ret; }
}

BUTree<? extends SExpr> ConditionalExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> left;
	BUTree<? extends SExpr> right;
}
{
  ret = ConditionalOrExpression()
  [
    LOOKAHEAD(2)
    { lateRun(); } "?" left = Expression() ":" right = ConditionalExpression()
    { ret = dress(SConditionalExpr.make(ret, left, right)); }
  ]
  { return ret; }
}

BUTree<? extends SExpr> ConditionalOrExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
}
{
  ret = ConditionalAndExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); } "||" right = ConditionalAndExpression()
    { ret = dress(SBinaryExpr.make(ret, BinaryOp.Or, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> ConditionalAndExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
}
{
  ret = InclusiveOrExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); } "&&" right = InclusiveOrExpression()
    { ret = dress(SBinaryExpr.make(ret, BinaryOp.And, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> InclusiveOrExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
}
{
  ret = ExclusiveOrExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); } "|" right = ExclusiveOrExpression()
    { ret = dress(SBinaryExpr.make(ret, BinaryOp.BinOr, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> ExclusiveOrExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
}
{
  ret = AndExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); } "^" right = AndExpression()
    { ret = dress(SBinaryExpr.make(ret, BinaryOp.XOr, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> AndExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
}
{
  ret = EqualityExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); } "&" right = EqualityExpression()
    { ret = dress(SBinaryExpr.make(ret, BinaryOp.BinAnd, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> EqualityExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
	BinaryOp op;
}
{
  ret = InstanceOfExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); }
    ( "==" { op = BinaryOp.Equal; } |
      "!=" { op = BinaryOp.NotEqual; } )
    right = InstanceOfExpression()
    { ret = dress(SBinaryExpr.make(ret, op, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> InstanceOfExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<SNodeList> annotations;
	BUTree<? extends SType> type;
}
{
  ret = RelationalExpression()
  [
    LOOKAHEAD(2)
    { lateRun(); } "instanceof"
    { run(); } annotations = Annotations() type = Type(annotations)
    { ret = dress(SInstanceOfExpr.make(ret, type)); }
  ]
  { return ret; }
}

BUTree<? extends SExpr> RelationalExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
	BinaryOp op;
}
{
  ret = ShiftExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); }
  	( "<"  { op = BinaryOp.Less; } |
  	  ">"  { op = BinaryOp.Greater; } |
  	  "<=" { op = BinaryOp.LessOrEqual; } |
  	  ">=" { op = BinaryOp.GreaterOrEqual; } )
  	right = ShiftExpression()
  	{ ret = dress(SBinaryExpr.make(ret, op, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> ShiftExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
	BinaryOp op;
}
{
  ret = AdditiveExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); }
  	( "<<"             { op = BinaryOp.LeftShift; } |
  	  RSIGNEDSHIFT()   { op = BinaryOp.RightSignedShift; } |
  	  RUNSIGNEDSHIFT() { op = BinaryOp.RightUnsignedShift; } )
  	right = AdditiveExpression()
  	{ ret = dress(SBinaryExpr.make(ret, op, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> AdditiveExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
	BinaryOp op;
}
{
  ret = MultiplicativeExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); }
  	( "+" { op = BinaryOp.Plus; } |
  	  "-" { op = BinaryOp.Minus; } )
  	right = MultiplicativeExpression()
  	{ ret = dress(SBinaryExpr.make(ret, op, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> MultiplicativeExpression():
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SExpr> right;
	BinaryOp op;
}
{
  ret = UnaryExpression()
  (
    LOOKAHEAD(2)
    { lateRun(); }
  	( "*" { op = BinaryOp.Times; } |
  	  "/" { op = BinaryOp.Divide; } |
  	  "%" { op = BinaryOp.Remainder; } )
  	right = UnaryExpression()
  	{ ret = dress(SBinaryExpr.make(ret, op, right)); }
  )*
  { return ret; }
}

BUTree<? extends SExpr> UnaryExpression():
{
	BUTree<? extends SExpr> ret;
	UnaryOp op;
}
{
  (
    ret = PrefixExpression()
  |
    { run(); }
    ( "+" { op = UnaryOp.Positive; } |
      "-" { op = UnaryOp.Negative; } )
    ret = UnaryExpression()
    { ret = dress(SUnaryExpr.make(op, ret)); }
  |
    ret = UnaryExpressionNotPlusMinus()
  )
  { return ret; }
}

BUTree<? extends SExpr> PrefixExpression():
{
	UnaryOp op;
	BUTree<? extends SExpr> ret;
}
{
  { run(); }
  ( "++" { op = UnaryOp.PreIncrement; } |
    "--" { op = UnaryOp.PreDecrement; } )
  ret = UnaryExpression()
  { return dress(SUnaryExpr.make(op, ret)); }
}

BUTree<? extends SExpr> UnaryExpressionNotPlusMinus():
{
	BUTree<? extends SExpr> ret;
	UnaryOp op;
}
{
  (
    { run(); }
	( "~" { op = UnaryOp.Inverse; } |
	  "!" { op = UnaryOp.Not; } )
	ret = UnaryExpression()
	{ ret = dress(SUnaryExpr.make(op, ret)); }
  |
    LOOKAHEAD(CastExpression())
//    LOOKAHEAD( "(", {isCast(false)} )
    ret = CastExpression()
  |
    ret = PostfixExpression()
  )
  { return ret; }
}

BUTree<? extends SExpr> PostfixExpression():
{
	BUTree<? extends SExpr> ret;
	UnaryOp op;
}
{
  ret = PrimaryExpression()
  [
  	LOOKAHEAD(2)
    { lateRun(); }
    ( "++" { op = UnaryOp.PostIncrement; } |
      "--" { op = UnaryOp.PostDecrement; } )
    { ret = dress(SUnaryExpr.make(op, ret)); }
  ]
  { return ret; }
}

BUTree<? extends SExpr> CastExpression():
{
	BUTree<SNodeList> annotations = null;
	BUTree<? extends SType> primitiveType;
	BUTree<? extends SType> type;
	BUTree<SNodeList> arrayDims;
	BUTree<? extends SExpr> ret;
}
{
  { run(); }
  "("
  { run(); } annotations = Annotations()
  (
    primitiveType = PrimitiveType(annotations)
    (
      ")"
      ret = UnaryExpression()
      { ret = dress(SCastExpr.make(primitiveType, ret)); }
    |
      { lateRun(); }
      arrayDims = ArrayDimsMandatory()
      { type = dress(SArrayType.make(primitiveType, arrayDims)); }
      type = ReferenceCastTypeRest(type)
      ")"
      ret = UnaryExpressionNotPlusMinus()
      { ret = dress(SCastExpr.make(type, ret)); }
    )
  |
    type = QualifiedType(annotations)
    [
      LOOKAHEAD( Annotations() "[" )
      { lateRun(); }
      arrayDims = ArrayDimsMandatory()
      { type = dress(SArrayType.make(type, arrayDims)); }
    ]
    type = ReferenceCastTypeRest(type)
    ")"
    ret = UnaryExpressionNotPlusMinus()
    { ret = dress(SCastExpr.make(type, ret)); }
  )
  { return ret; }
}

BUTree<? extends SType> ReferenceCastTypeRest(BUTree<? extends SType> type):
{
	BUTree<SNodeList> types = emptyList();
	BUTree<SNodeList> annotations = null;
}
{
  [
    LOOKAHEAD("&")
    { types = append(types, type); }
    { lateRun(); }
    (
      "&"
      { run(); } annotations = Annotations() type = ReferenceType(annotations)
      { types = append(types, type); }
    )+
    { type = dress(SIntersectionType.make(types)); }
  ]
  { return type; }
}

BUTree<? extends SExpr> Literal():
{
	BUTree<? extends SExpr> ret;
}
{
  { run(); }
  (
    <INTEGER_LITERAL>   { ret = SLiteralExpr.make(Integer.class, token.image); }
  |
    <LONG_LITERAL>      { ret = SLiteralExpr.make(Long.class, token.image); }
  |
    <FLOAT_LITERAL>     { ret = SLiteralExpr.make(Float.class, token.image); }
  |
    <DOUBLE_LITERAL>    { ret = SLiteralExpr.make(Double.class, token.image); }
  |
    <CHARACTER_LITERAL> { ret = SLiteralExpr.make(Character.class, token.image); }
  |
    <STRING_LITERAL>    { ret = SLiteralExpr.make(String.class, token.image); }
  |
    "true"  { ret = SLiteralExpr.make(Boolean.class, token.image); }
  |
    "false" { ret = SLiteralExpr.make(Boolean.class, token.image); }
  |
    "null"  { ret = SLiteralExpr.make(Void.class, token.image); }
  )
  { return dress(ret); }
}

BUTree<? extends SExpr> PrimaryExpression():
{
	BUTree<? extends SExpr> ret;
}
{
  ret = PrimaryPrefix()
  (
    LOOKAHEAD(2)
    { lateRun(); } ret = PrimarySuffix(ret)
  )*
  { return ret; }
}

BUTree<? extends SExpr> PrimaryExpressionWithoutSuperSuffix():
{
	BUTree<? extends SExpr> ret;
}
{
  ret = PrimaryPrefix()
  (
    LOOKAHEAD( PrimarySuffixWithoutSuper() )
    { lateRun(); } ret = PrimarySuffixWithoutSuper(ret)
  )*
  { return ret; }
}

BUTree<? extends SExpr> PrimaryPrefix():
{
	BUTree<? extends SExpr> ret = null;
	BUTree<SNodeList> typeArgs = null;
	BUTree<SNodeList> params;
	BUTree<? extends SType> type;
}
{
  (
    ret = Literal()
  |
    { run(); } "this" { ret = dress(SThisExpr.make(none())); }
  |
    { run(); } "super" { ret = dress(SSuperExpr.make(none())); }
	(
	  { lateRun(); }
	  "."
	  (
        LOOKAHEAD( [ TypeArguments() ] Name() "(" )
        ret = MethodInvocation(ret)
	  |
	    ret = FieldAccess(ret)
	  )
	|
	  { lateRun(); }
	  ret = MethodReferenceSuffix(ret)
	)
  |
    ret = AllocationExpression(null)
  |
    LOOKAHEAD( ResultType() "." "class" )
    { run(); } type = ResultType() "." "class" { ret = dress(SClassExpr.make(type)); }
  |
    LOOKAHEAD (ResultType() "::" )
    { run(); }
    type = ResultType() { ret = STypeExpr.make(type); }
    ret = MethodReferenceSuffix(ret)
  |
    LOOKAHEAD( [ TypeArguments() ] Name() "(" )
    { run(); }
    ret = MethodInvocation(null)
  |
    ret = Name()
    [
      { lateRun(); }
      "->" ret = LambdaBody(singletonList(makeFormalParameter((BUTree<SName>) ret)), false)
    ]
  |
    { run(); }
    "("
    (
      ")" "->" ret = LambdaBody(emptyList(), true)
    |
      LOOKAHEAD( Name() ")" "->" )
      ret = Name() ")" "->" ret = LambdaBody(singletonList(makeFormalParameter((BUTree<SName>) ret)), true)
    |
      LOOKAHEAD( Name() "," )
      params = InferredFormalParameterList() ")" "->" ret = LambdaBody(params, true)
    |
      LOOKAHEAD({ isLambda(true) })
      params = FormalParameterList() ")" "->" ret = LambdaBody(params, true)
    |
      ret = Expression() ")"
      { ret = dress(SParenthesizedExpr.make(ret)); }
    )
  )
  { return ret; }
}

BUTree<? extends SExpr> PrimarySuffix(BUTree<? extends SExpr> scope):
{
	BUTree<? extends SExpr> ret;
}
{
  // Run is started before scope

  (
    LOOKAHEAD(2)
    ret = PrimarySuffixWithoutSuper(scope)
  |
    "." "super" { ret = dress(SSuperExpr.make(optionOf(scope))); }
  |
    ret = MethodReferenceSuffix(scope)
  )
  { return ret; }
}

BUTree<? extends SExpr> PrimarySuffixWithoutSuper(BUTree<? extends SExpr> scope):
{
	BUTree<? extends SExpr> ret;
	BUTree<SName> name;
}
{
  // Run is started before scope

  (
    "."
    (
      "this" { ret = dress(SThisExpr.make(optionOf(scope))); }
    |
      ret = AllocationExpression(scope)
    |
      LOOKAHEAD( [ TypeArguments() ] Name() "(" )
      ret = MethodInvocation(scope)
    |
      ret = FieldAccess(scope)
    )
  |
    "[" ret = Expression() "]" { ret = dress(SArrayAccessExpr.make(scope, ret)); }
  )

  { return ret; }
}

BUTree<? extends SExpr> FieldAccess(BUTree<? extends SExpr> scope):
{
	BUTree<SName> name;
}
{
  // Run is started before scope

  name = Name()
  { return dress(SFieldAccessExpr.make(optionOf(scope), name)); }
}

BUTree<? extends SExpr> MethodInvocation(BUTree<? extends SExpr> scope):
{
    BUTree<SNodeList> typeArgs = null;
	BUTree<SName> name;
    BUTree<SNodeList> args = null;
	BUTree<? extends SExpr> ret;
}
{
  // Run is started before scope

  [ typeArgs = TypeArguments() ] name = Name()
  args = Arguments()
  { return dress(SMethodInvocationExpr.make(optionOf(scope), ensureNotNull(typeArgs), name, args)); }
}

BUTree<SNodeList> Arguments():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SExpr> expr;
}
{
  "("
  [
    LOOKAHEAD(1)
    expr = Expression() { ret = append(ret, expr); }
    ( "," expr = Expression() { ret = append(ret, expr); } )*
  |
    LOOKAHEAD({quotesMode}) ret = NodeListVar()
  ]
  ")"
  { return ret; }
}

BUTree<? extends SExpr> MethodReferenceSuffix(BUTree<? extends SExpr> scope):
{
    BUTree<SNodeList> typeArgs = null;
	BUTree<SName> name;
	BUTree<? extends SExpr> ret;
}
{
  // Run is started before scope

  "::" [typeArgs = TypeArguments() ]
  ( name = Name() | "new" { name = SName.make("new"); } )
  { ret = dress(SMethodReferenceExpr.make(scope, ensureNotNull(typeArgs), name)); }
  { return ret; }
}

BUTree<? extends SExpr> AllocationExpression(BUTree<? extends SExpr> scope):
{
	BUTree<? extends SExpr> ret;
	BUTree<? extends SType> type;
	BUTree<SNodeList> typeArgs = null;
	BUTree<SNodeList> anonymousBody = null;
	BUTree<SNodeList> args;
	BUTree<SNodeList> annotations = null;
}
{
  // Run may be started before scope
  { if (scope == null) run(); }

  "new"
  [ typeArgs = TypeArguments() ]

  { run(); }
  annotations = Annotations()
  (
    type = PrimitiveType(annotations)
    ret = ArrayCreationExpr(type)
  |
	type = QualifiedType(annotations)
    (
	  ret = ArrayCreationExpr(type)
	|
	  args = Arguments() [ LOOKAHEAD( "{" ) anonymousBody = ClassOrInterfaceBody(TypeKind.Class) ]
      { ret = dress(SObjectCreationExpr.make(optionOf(scope), ensureNotNull(typeArgs), (BUTree<SQualifiedType>) type, args, optionOf(anonymousBody))); }
    )
  )
  { return ret; }
}

BUTree<? extends SExpr> ArrayCreationExpr(BUTree<? extends SType> componentType):
{
	BUTree<? extends SExpr> expr;
	BUTree<SNodeList> arrayDimExprs = emptyList();
	BUTree<SNodeList> arrayDims = emptyList();
	BUTree<SNodeList> annotations = null;
	BUTree<SArrayInitializerExpr> initializer;
}
{
  (
    LOOKAHEAD( Annotations() "[" Expression() "]" )
    arrayDimExprs = ArrayDimExprsMandatory()
    arrayDims = ArrayDims()
    { return dress(SArrayCreationExpr.make(componentType, arrayDimExprs, arrayDims, none())); }
  |
    arrayDims = ArrayDimsMandatory()
    initializer = ArrayInitializer()
    { return dress(SArrayCreationExpr.make(componentType, arrayDimExprs, arrayDims, optionOf(initializer))); }
  )
}

BUTree<SNodeList> ArrayDimExprsMandatory():
{
	BUTree<SNodeList> arrayDimExprs = emptyList();
	BUTree<SNodeList> annotations;
	BUTree<? extends SExpr> expr;
}
{
  (
    LOOKAHEAD( Annotations() "[" Expression() "]" )
    { run(); }
    annotations = Annotations() "[" expr = Expression() "]"
    { arrayDimExprs = append(arrayDimExprs, dress(SArrayDimExpr.make(annotations, expr))); }
  )+
  { return arrayDimExprs; }
}

BUTree<SNodeList> ArrayDimsMandatory():
{
	BUTree<SNodeList> arrayDims = emptyList();
	BUTree<SNodeList> annotations;
}
{
  (
    LOOKAHEAD( Annotations() "[" "]" )
    { run(); }
    annotations = Annotations() "[" "]"
    { arrayDims = append(arrayDims, dress(SArrayDim.make(annotations))); }
  )+
  { return arrayDims; }
}

/**********************************************************************************************************************
 * Statements
 **********************************************************************************************************************/

BUTree<? extends SStmt> Statement():
{
	BUTree<? extends SStmt> ret;
}
{
  (
	  LOOKAHEAD(2)
	  ret = LabeledStatement()
	|
	  ret = AssertStatement()
	|
	  ret = Block()
	|
	  ret = EmptyStatement()
	|
	  ret = StatementExpression()
	|
	  ret = SwitchStatement()
	|
	  ret = IfStatement()
	|
	  ret = WhileStatement()
	|
	  ret = DoStatement()
	|
	  ret = ForStatement()
	|
	  ret = BreakStatement()
	|
	  ret = ContinueStatement()
	|
	  ret = ReturnStatement()
	|
	  ret = ThrowStatement()
	|
	  ret = SynchronizedStatement()
	|
	  ret = TryStatement()
  )
  { return ret; }
}

BUTree<SAssertStmt> AssertStatement():
{
	BUTree<? extends SExpr> check;
	BUTree<? extends SExpr> msg = null;
}
{
  { run(); }
  "assert" check = Expression() [ ":" msg = Expression() ] ";"
  { return dress(SAssertStmt.make(check, optionOf(msg)));  }
}

BUTree<SLabeledStmt> LabeledStatement():
{
	BUTree<SName> label;
	BUTree<? extends SStmt> stmt;
}
{
  { run(); }
  label = Name() ":" stmt = Statement()
  { return dress(SLabeledStmt.make(label, stmt)); }
}

BUTree<SBlockStmt> Block():
{
	BUTree<SNodeList> stmts;
}
{
  { run(); }
  "{" stmts = Statements() "}"
  { return dress(SBlockStmt.make(ensureNotNull(stmts))); }
}

/*
 * Classes inside block stametents can only be abstract or final. The semantic must check it.
 */
BUTree<? extends SStmt> BlockStatement():
{
	BUTree<? extends SStmt> ret;
	BUTree<? extends SExpr> expr;
	BUTree<? extends STypeDecl> typeDecl;
	BUTree<SNodeList> modifiers;
}
{
  (
    LOOKAHEAD( ModifiersNoDefault() ("class" | "interface") )
    { run(); }
    { run(); } modifiers = ModifiersNoDefault() typeDecl = ClassOrInterfaceDecl(modifiers)
    { ret = dress(STypeDeclarationStmt.make(typeDecl)); }
  |
    LOOKAHEAD( VariableDeclExpression() )
    { run(); }
    expr = VariableDeclExpression() ";"
    { ret = dress(SExpressionStmt.make(expr)); }
  |
    ret = Statement()
  )
  { return ret; }
}

BUTree<SVariableDeclarationExpr> VariableDeclExpression():
{
	BUTree<SNodeList> modifiers;
	BUTree<SLocalVariableDecl> variableDecl;
}
{
  { run(); }
  { run(); } modifiers = ModifiersNoDefault() variableDecl = VariableDecl(modifiers)
  { return dress(SVariableDeclarationExpr.make(variableDecl)); }
}

BUTree<SEmptyStmt> EmptyStatement():
{}
{
  { run(); }
  ";"
  { return dress(SEmptyStmt.make()); }
}

BUTree<SExpressionStmt> StatementExpression():
{
	BUTree<? extends SExpr> expr;
	AssignOp op;
	BUTree<? extends SExpr> value;
}
{
  { run(); }
  (
    expr = PrefixExpression()
  |
    expr = PrimaryExpression()
    [
      { lateRun(); } "++"
      { expr = dress(SUnaryExpr.make(UnaryOp.PostIncrement, expr));  }
    |
      { lateRun(); } "--"
      { expr = dress(SUnaryExpr.make(UnaryOp.PostDecrement, expr));  }
    |
      { lateRun(); } op = AssignmentOperator() value = Expression()
      { expr = dress(SAssignExpr.make(expr, op, value)); }
    ]
  )
  ";"
  { return dress(SExpressionStmt.make(expr)); }
}

BUTree<SSwitchStmt> SwitchStatement():
{
	BUTree<? extends SExpr> selector;
	BUTree<SSwitchCase> entry;
	BUTree<SNodeList> entries = emptyList();
}
{
  { run(); }
  "switch" "(" selector = Expression() ")" "{"
  ( entry = SwitchEntry() { entries = append(entries, entry); } )*
  "}"
  { return dress(SSwitchStmt.make(selector, entries)); }
}

BUTree<SSwitchCase> SwitchEntry():
{
	BUTree<? extends SExpr> label = null;
	BUTree<SNodeList> stmts;
}
{
  { run(); }
  (
    "case" label = Expression()
  |
    "default"
  )
  ":" stmts = Statements()
  { return dress(SSwitchCase.make(optionOf(label), ensureNotNull(stmts))); }
}

BUTree<SIfStmt> IfStatement():
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if Stmt.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 */
{
	BUTree<? extends SExpr> condition;
	BUTree<? extends SStmt> thenStmt;
	BUTree<? extends SStmt> elseStmt = null;
}
{
  { run(); }
  "if" "(" condition = Expression() ")" thenStmt = Statement() [ LOOKAHEAD(1) "else" elseStmt = Statement() ]
  { return dress(SIfStmt.make(condition, thenStmt, optionOf(elseStmt))); }
}

BUTree<SWhileStmt> WhileStatement():
{
	BUTree<? extends SExpr> condition;
	BUTree<? extends SStmt> body;
}
{
  { run(); }
  "while" "(" condition = Expression() ")" body = Statement()
  { return dress(SWhileStmt.make(condition, body)); }
}

BUTree<SDoStmt> DoStatement():
{
	BUTree<? extends SExpr> condition;
	BUTree<? extends SStmt> body;
}
{
  { run(); }
  "do" body = Statement() "while" "(" condition = Expression() ")" ";"
  { return dress(SDoStmt.make(body, condition)); }
}

BUTree<? extends SStmt> ForStatement():
{
	BUTree<SVariableDeclarationExpr> varExpr = null;
	BUTree<? extends SExpr> expr = null;
	BUTree<SNodeList> init = null;
	BUTree<SNodeList> update = null;
	BUTree<? extends SStmt> body;
}
{
  { run(); }
  "for" "("
  (
    LOOKAHEAD(VariableDeclExpression() ":")
    varExpr = VariableDeclExpression() ":" expr = Expression()
  |
    [ init = ForInit() ] ";" [ expr = Expression() ] ";" [ update = ForUpdate() ]
  )
  ")" body = Statement()
  {
  	if (varExpr != null) return dress(SForeachStmt.make(varExpr, expr, body));
    else return dress(SForStmt.make(init, expr, update, body));
  }
}

BUTree<SNodeList> ForInit():
{
	BUTree<SNodeList> ret;
	BUTree<? extends SExpr> expr;
}
{
  (
    LOOKAHEAD( Modifiers() Type() Name() )
    expr = VariableDeclExpression() { ret = emptyList(); ret = append(ret, expr); }
  |
    ret = ExpressionList()
  )
  { return ret; }
}

BUTree<SNodeList> ExpressionList():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SExpr> expr;
}
{
  expr = Expression() { ret = append(ret, expr); } ( "," expr = Expression()  { ret = append(ret, expr); } )*
  { return ret; }
}

BUTree<SNodeList> ForUpdate():
{
	BUTree<SNodeList> ret;
}
{
  ret = ExpressionList()
  { return ret; }
}

BUTree<SBreakStmt> BreakStatement():
{
	BUTree<SName> id = null;
}
{
  { run(); }
  "break" [ id = Name() ] ";"
  { return dress(SBreakStmt.make(optionOf(id))); }
}

BUTree<SContinueStmt> ContinueStatement():
{
	BUTree<SName> id = null;
}
{
  { run(); }
  "continue" [ id = Name() ] ";"
  { return dress(SContinueStmt.make(optionOf(id))); }
}

BUTree<SReturnStmt> ReturnStatement():
{
	BUTree<? extends SExpr> expr = null;
}
{
  { run(); }
  "return" [ expr = Expression() ] ";"
  { return dress(SReturnStmt.make(optionOf(expr))); }
}

BUTree<SThrowStmt> ThrowStatement():
{
	BUTree<? extends SExpr> expr;
}
{
  { run(); }
  "throw" expr = Expression() ";"
  { return dress(SThrowStmt.make(expr)); }
}

BUTree<SSynchronizedStmt> SynchronizedStatement():
{
	BUTree<? extends SExpr> expr;
	BUTree<SBlockStmt> block;
}
{
  { run(); }
  "synchronized" "(" expr = Expression() ")" block = Block()
  { return dress(SSynchronizedStmt.make(expr, block)); }
}

BUTree<STryStmt> TryStatement():
// TODO Make catchClauses/finallyClause counts less fatal
{
	BUTree<SNodeList> resources = null;
	ByRef<Boolean> trailingSemiColon = new ByRef<Boolean>(false);
	BUTree<SBlockStmt> tryBlock;
	BUTree<SBlockStmt> finallyBlock = null;
	BUTree<SNodeList> catchClauses = null;
}
{
  { run(); }
  "try"
  (
    resources = ResourceSpecification(trailingSemiColon)
    tryBlock = Block()
    [ catchClauses = CatchClauses() ]
    [ "finally" finallyBlock = Block() ]
  |
    tryBlock = Block()
    (
      catchClauses = CatchClauses()
      [ "finally" finallyBlock = Block() ]
    |
      "finally" finallyBlock = Block()
    )
  )
  { return dress(STryStmt.make(ensureNotNull(resources), trailingSemiColon.value, tryBlock, ensureNotNull(catchClauses), optionOf(finallyBlock))); }
}

BUTree<SNodeList> CatchClauses():
{
	BUTree<SNodeList> catchClauses = emptyList();
	BUTree<SCatchClause> catchClause;
}
{
  (
    catchClause = CatchClause() { catchClauses = append(catchClauses, catchClause); }
  )+
  { return catchClauses; }
}

BUTree<SCatchClause> CatchClause():
{
	BUTree<SFormalParameter> param;
	BUTree<SBlockStmt> catchBlock;
}
{
  { run(); }
  "catch" "(" param = CatchFormalParameter() ")" catchBlock = Block()
  { return dress(SCatchClause.make(param, catchBlock)); }
}

BUTree<SFormalParameter> CatchFormalParameter():
{
	BUTree<SNodeList> modifiers;
	BUTree<? extends SType> exceptType;
	BUTree<SNodeList> exceptTypes = emptyList();
	BUTree<SVariableDeclaratorId> exceptId;
}
{
  { run(); }
  modifiers = Modifiers()

  exceptType = QualifiedType(null) { exceptTypes = append(exceptTypes, exceptType); }
  [
    LOOKAHEAD("|")
    { lateRun(); }
    ( "|" exceptType = AnnotatedQualifiedType() { exceptTypes = append(exceptTypes, exceptType); } )+
    { exceptType = dress(SUnionType.make(exceptTypes)); }
  ]

  exceptId = VariableDeclaratorId()
  { return dress(SFormalParameter.make(modifiers, exceptType, false, exceptId)); }
}

BUTree<SNodeList> ResourceSpecification(ByRef<Boolean> trailingSemiColon) :
{
	BUTree<SNodeList> vars = emptyList();
	BUTree<SVariableDeclarationExpr> var;
}
{
  "("
  // FIXME this is a bit more lenient than we need to be, eg allowing access modifiers like private
  var = VariableDeclExpression() { vars = append(vars, var); }
  ( LOOKAHEAD(2) ";" var = VariableDeclExpression() {vars = append(vars, var);} )*
  [ LOOKAHEAD(2) ";" { trailingSemiColon.value = true; } ]
  ")"
  { return vars; }
}


/* We use productions to match >>>, >> and > so that we can keep the
 * type Decl syntax with generics clean
 */

void RUNSIGNEDSHIFT():
{}
{
  ( LOOKAHEAD({ getToken(1).kind == GT &&
                getToken(1).realKind == RUNSIGNEDSHIFT } )
   ">" ">" ">" { popNewWhitespaces(); }
  )
}

void RSIGNEDSHIFT():
{}
{
  ( LOOKAHEAD({ getToken(1).kind == GT &&
                getToken(1).realKind == RSIGNEDSHIFT } )
  ">" ">" { popNewWhitespaces(); }
  )
}


/**********************************************************************************************************************
 * Annotations
 **********************************************************************************************************************/

BUTree<SNodeList> Annotations():
{
	BUTree<SNodeList> annotations = emptyList();
	BUTree<? extends SAnnotationExpr> annotation;
}
{
  ( annotation = Annotation() { annotations = append(annotations, annotation); } )*
  { return annotations; }
}

BUTree<? extends SAnnotationExpr> Annotation():
{
	BUTree<? extends SAnnotationExpr> ret;
}
{
  (
    LOOKAHEAD( "@" QualifiedName() "(" ( Name() "=" | ")" ))
    ret = NormalAnnotation()
  |
    LOOKAHEAD( "@" QualifiedName() "(" )
    ret = SingleMemberAnnotation()
  |
    LOOKAHEAD( "@" QualifiedName() )
    ret = MarkerAnnotation()
  )
  { return ret; }
}

BUTree<SNormalAnnotationExpr> NormalAnnotation():
{
	BUTree<SQualifiedName> name;
	BUTree<SNodeList> pairs = null;
}
{
  { run(); }
  "@" name = QualifiedName() "(" [ pairs = MemberValuePairs() ] ")"
  { return dress(SNormalAnnotationExpr.make(name, ensureNotNull(pairs))); }
}

BUTree<SMarkerAnnotationExpr> MarkerAnnotation():
{
	BUTree<SQualifiedName> name;
}
{
  { run(); }
  "@" name = QualifiedName()
  { return dress(SMarkerAnnotationExpr.make(name)); }
}

BUTree<SSingleMemberAnnotationExpr> SingleMemberAnnotation():
{
	BUTree<SQualifiedName> name;
	BUTree<? extends SExpr> memberVal;
}
{
  { run(); }
  "@" name = QualifiedName() "(" memberVal = MemberValue() ")"
  { return dress(SSingleMemberAnnotationExpr.make(name, memberVal)); }
}

BUTree<SNodeList> MemberValuePairs():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<SMemberValuePair> pair;
}
{
  pair = MemberValuePair() { ret = append(ret, pair); }
  ( "," pair = MemberValuePair() { ret = append(ret, pair); } )*
  { return ret; }
}

BUTree<SMemberValuePair> MemberValuePair():
{
	BUTree<SName> name;
	BUTree<? extends SExpr> value;
}
{
  { run(); }
  name = Name() "=" value = MemberValue()
  { return dress(SMemberValuePair.make(name, value)); }
}

BUTree<? extends SExpr> MemberValue():
{
	BUTree<? extends SExpr> ret;
}
{
  (
    ret = Annotation()
  |
    ret = MemberValueArrayInitializer()
  |
    ret = ConditionalExpression()
  )
  { return ret; }
}

BUTree<? extends SExpr> MemberValueArrayInitializer():
{
	BUTree<SNodeList> ret = emptyList();
	BUTree<? extends SExpr> member;
	boolean trailingComma = false;
}
{
  { run(); }
  "{"
  [
    member = MemberValue() { ret = append(ret, member); }
    ( LOOKAHEAD(2) "," member = MemberValue() { ret = append(ret, member); } )*
  ]
  [ "," { trailingComma = true; } ]
  "}"
  { return dress(SArrayInitializerExpr.make(ret, trailingComma)); }
}
